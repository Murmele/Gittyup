# Maintainer:  Tim Schumacher <timschumi@gmx.de>
# Contributor: KillWolfVlad <github.com/KillWolfVlad>
# Contributor: WaveHack <email@wavehack.net>
# Contributor: Whovian9369 <Whovian9369@gmail.com>
# Contributor: Angelo Theodorou <encelo@gmail.com>
# Contributor: Grafcube <grafcube@protonmail.com>

pkgname=gittyup-git
pkgver=r504.410be91
pkgrel=1
pkgdesc="A graphical Git client designed to help you understand and manage your source code history. Successor of Gitahead."
url="https://github.com/Murmele/Gittyup"
arch=('x86_64')
license=('MIT')
depends=('desktop-file-utils' 'qt5-base' 'git')
makedepends=('cmake' 'ninja' 'git' 'qt5-tools' 'qt5-translations')
source=($pkgname::git+https://github.com/Murmele/gittyup.git)
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$pkgname"

  git submodule update --init --recursive
}

build() {
  if [ ! -d "$srcdir/$pkgname/build/release" ]; then
    mkdir -p "$srcdir/$pkgname/build/release"
  fi
  cd "$srcdir/$pkgname/build/release"

  cmake -G Ninja -DCMAKE_BUILD_TYPE=Release -DCMAKE_PREFIX_PATH=/usr/lib ../..
  ninja
}

package() {
  cd "$srcdir/$pkgname/build/release"

  ninja package

  mkdir -p "${pkgdir}/usr/"{share,bin}

  rm "${srcdir}/${pkgname}/build/release/_CPack_Packages/Linux/STGZ/"Gittyup-*.sh

  cp -r "${srcdir}/${pkgname}/build/release/_CPack_Packages/Linux/STGZ/Gittyup-"* "${pkgdir}/usr/share/gittyup"

  rm -rf "${pkgdir}/usr/share/gittyup/"*.so.*

  ln -s "/usr/share/gittyup/Gittyup" "${pkgdir}/usr/bin/gittyup"

  install -Dm644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/gittyup_logo.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/gittyup.svg"
  install -Dm644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_16x16.png" "${pkgdir}/usr/share/icons/hicolor/16x16/apps/gittyup.png"
  install -Dm644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_32x32.png" "${pkgdir}/usr/share/icons/hicolor/32x32/apps/gittyup.png"
  install -Dm644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_64x64.png" "${pkgdir}/usr/share/icons/hicolor/64x64/apps/gittyup.png"
  install -Dm644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_128x128.png" "${pkgdir}/usr/share/icons/hicolor/128x128/apps/gittyup.png"
  install -Dm644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_256x256.png" "${pkgdir}/usr/share/icons/hicolor/256x256/apps/gittyup.png"
  install -Dm644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_512x512.png" "${pkgdir}/usr/share/icons/hicolor/512x512/apps/gittyup.png"

  install -Dm644 ${srcdir}/${pkgname}/LICENSE.md "${pkgdir}/usr/share/licenses/gittyup/LICENSE"
  install -Dm755 "${srcdir}/${pkgname}/rsrc/linux/com.github.Murmele.Gittyup.desktop" "${pkgdir}/usr/share/applications/com.github.Murmele.Gittyup.desktop"
}
